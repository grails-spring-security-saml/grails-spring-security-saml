buildscript {
    repositories {
        mavenCentral()
        maven { url "https://repo.grails.org/grails/restricted" }
        maven {
            url = 'https://repository.apache.org/content/groups/snapshots'
            content {
                includeVersionByRegex('org[.]apache[.](grails|groovy).*', '.*', '.*-SNAPSHOT')
            }
        }
    }
    dependencies {
        classpath platform("org.apache.grails:grails-bom:$grailsVersion")
        classpath "org.apache.grails:grails-gradle-plugins"
        classpath "cloud.wondrify:asset-pipeline-gradle"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.28.3"
    }
}

apply plugin:"application"
apply plugin:"com.jfrog.artifactory"
apply plugin:"maven-publish"
apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.apache.grails.gradle.grails-plugin"
apply plugin:"cloud.wondrify.asset-pipeline"
apply plugin:"org.apache.grails.gradle.grails-gsp"
apply plugin:"war"
apply plugin:'signing'
apply plugin:'com.jfrog.artifactory'
apply plugin:'io.github.gradle-nexus.publish-plugin'

group = "io.github.grails-spring-security-saml"

repositories {
    mavenCentral()
    maven { url "https://repo.grails.org/grails/restricted" }
    maven {
        url = 'https://repository.apache.org/content/groups/snapshots'
        content {
            includeVersionByRegex('org[.]apache[.](grails|groovy).*', '.*', '.*-SNAPSHOT')
        }
    }
    maven { url "https://repo1.maven.org/maven2/"}
    maven { url "https://build.shibboleth.net/maven/releases/"}
}

configurations {
    all {
    }
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    console "org.apache.grails:grails-console"
    implementation platform("org.apache.grails:grails-bom:$grailsVersion")

    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.apache.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.apache.grails:grails-web-boot"
    implementation "org.apache.grails:grails-logging"
    // implementation "org.apache.grails:grails-plugin-rest"
    implementation "org.apache.grails:grails-databinding"
    implementation "org.apache.grails:grails-services"
    implementation "org.apache.grails:grails-url-mappings"
    implementation "org.apache.grails:grails-interceptors"
    implementation "org.apache.grails:grails-cache"
    implementation "org.apache.grails:grails-scaffolding"
    implementation "org.apache.grails:grails-gsp"

    implementation 'org.apache.grails:grails-spring-security:7.0.0'
    implementation 'org.springframework.security:spring-security-saml2-service-provider'
    implementation 'org.opensaml:opensaml-core:4.3.2'
    implementation 'org.opensaml:opensaml-saml-api:4.3.2'
    implementation 'org.opensaml:opensaml-saml-impl:4.3.2'

    integrationTestImplementation testFixtures("org.apache.grails:grails-geb")
    profile "org.apache.grails.profiles:web"

    runtimeOnly "cloud.wondrify:asset-pipeline-grails"
    testAndDevelopmentOnly "org.webjars.npm:bootstrap"
    testAndDevelopmentOnly "org.webjars.npm:bootstrap-icons"
    testAndDevelopmentOnly "org.webjars.npm:jquery"
    testImplementation "org.apache.grails:grails-testing-support-datamapping"
    testImplementation "org.spockframework:spock-core"
    testImplementation "org.apache.grails:grails-testing-support-web"
    implementation "joda-time:joda-time:2.14.0"
}

application {
    mainClass.set("org.grails.plugin.springsecurity.saml.Application")
}

compileJava.options.release = 17

tasks.withType(Test) {
    useJUnitPlatform()
}

bootRun {
    jvmArgs(
        '-Dspring.output.ansi.enabled=always',
        '-noverify',
        '-XX:TieredStopAtLevel=1',
        '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}
bootJar.enabled = false

ext."signing.keyId" = project.hasProperty("signing.keyId") ? project.getProperty('signing.keyId') : System.getenv('SIGNING_KEY_ID')
ext."signing.password" = project.hasProperty("signing.password") ? project.getProperty('signing.password') : System.getenv('SIGNING_PASSPHRASE')
ext."signing.secretKeyRingFile" = project.hasProperty("signing.secretKeyRingFile") ? project.getProperty('signing.secretKeyRingFile') : (System.getenv('SIGNING_SECRING')?:"${System.properties['user.home']}${File.separator}.gnupg${File.separator}secring.gpg")

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

afterEvaluate {
   signing {
       required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
       sign publishing.publications.maven
   }
}

tasks.withType(Sign) {
   onlyIf { isReleaseVersion }
}

java {
    withSourcesJar()   // creates `sourcesJar` task
    withJavadocJar()   // creates `javadocJar` task
}

publishing {
   publications {
       maven(MavenPublication) {
           groupId = project.group
           artifactId = rootProject.name
           version = project.version

           from components.java

           pom {
               name = 'Grails Spring Security SAML2.0 Plugin for Grails'
               description = 'The Plugin basically creates a bridge from your Grails application configuration ' + \
                            'to both the Spring Security SAML service provider library and the Grails Spring Security Plugin. ' + \
                            'Instead of having to map all of the beans in your application, the plugin wires the SAML service provider ' + \
                            'beans based on your application configuration.'
               url = 'https://github.com/grails-spring-security-saml/grails-spring-security-saml'
               licenses {
                   license {
                       name = 'The Apache License, Version 2.0'
                       url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                   }
               }
               developers {
                   developer {
                       id = 'valentingoebel'
                       name = 'Valentin GÃ¶bel'
                       email = 'valentin.goebel@tu-braunschweig.de'
                   }
               }
               scm {
                   connection = 'scm:git:git://github.com/grails-spring-security-saml/grails-spring-security-saml.git'
                   developerConnection = 'scm:git:ssh://github.com:grails-spring-security-saml/grails-spring-security-saml.git'
                   url = 'https://github.com/grails-spring-security-saml/grails-spring-security-saml/tree/main'
               }
           }
       }
   }
}

// https://grails.org/blog/2021-04-07-publish-grails-plugin-to-maven-central.html
nexusPublishing {
   repositories {
       sonatype {
           def ossUser = System.getenv("SONATYPE_USERNAME") ?: project.hasProperty("sonatypeOssUsername") ? project.sonatypeOssUsername : ''
           def ossPass = System.getenv("SONATYPE_PASSWORD") ?: project.hasProperty("sonatypeOssPassword") ? project.sonatypeOssPassword : ''
           def ossStagingProfileId = System.getenv("SONATYPE_STAGING_PROFILE_ID") ?: project.hasProperty("sonatypeOssStagingProfileId") ? project.sonatypeOssStagingProfileId : ''
           nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
           snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
           username = ossUser
           password = ossPass
           stagingProfileId = ossStagingProfileId
       }
   }
}

artifactory {
    contextUrl = "https://grailsrepo.rz.tu-bs.de/artifactory/"
    publish {
        repository {
            repoKey = 'tubs-grails'
            username = System.getenv("GRAILSREPO_USER")
            password = System.getenv("GRAILSREPO_PASS")
            //maven = true
        }
        defaults {
            publications('maven')
        }
    }
    /*resolve {
        repository {
            repoKey = 'tubs-grails'
            username = System.getenv("GRAILSREPO_USER")
            password = System.getenv("GRAILSREPO_PASS")
            //maven = true
        }
    }*/
}